=== Getting Started with the ClojureScript compiler

At this point, you are surely very bored with the constant theoretical explanations about the language
itself and will want to write and execute some code. The goal of this section is to provide a little practical
introduction to the _ClojureScript_ compiler.

The _ClojureScript_ compiler takes the source code that has been split over numerous directories and namespaces and
compiles it down to JavaScript. Today, JavaScript has a great number of different environments
where it can be executed - each with its own peculiarities.

This chapter intends to explain how to use _ClojureScript_ without any additional tooling. This
will help you understand how the compiler works and how you can use it when other tooling is not
available (such as link:http://leiningen.org/[leiningen] + link:https://github.com/emezeske/lein-cljsbuild[cljsbuild]
or link:http://boot-clj.com/[boot]).


==== Execution environments

What is an execution environment? An execution environment is an engine where JavaScript can be
executed. For example, the most popular execution environment is a browser (Chrome, Firefox, ...)
followed by the second most popular - link:https://nodejs.org/[nodejs]/link:https://iojs.org/en/index.html[iojs].

There are others, such as Rhino (JDK 6+), Nashorn (JDK 8), QtQuick (QT),... but none of them have
significant differences from the first two. So, _ClojureScript_ at the moment may compile
code to run in the browser or in nodejs/iojs-like environments out of the box.


==== Download the compiler

The _ClojureScript_ compiler is implemented in Java. To use it, you should have jdk8 installed.
_ClojureScript_ itself only requires JDK 7, but the standalone compiler that we are going to use in this
chapter requires JDK 8, which can be found at
http://www.oracle.com/technetwork/java/javase/downloads/jdk8-downloads-2133151.html

You can download it using `wget`:

[source, bash]
----
wget https://github.com/clojure/clojurescript/releases/download/r3308/cljs.jar
----

The _ClojureScript_ compiler is packaged in a standalone executable jar file, so this is the only
file (along with JDK 8) that you need to compile your _ClojureScript_ source code to JavaScript.


==== Compile for nodejs/iojs

Let’s start with a practical example compiling code that will target *nodejs*/*iojs*. For
this example, you should have nodejs or iojs (recommended) installed.

There are different ways to install iojs, but the recommended way is using nvm (node version
manager). You can read the instructions on how to install and use nvm on its
link:https://github.com/creationix/nvm[home page].

You can test if *iojs* is installed in your system with this command:

[source, shell]
----
$ iojs --version
v2.3.4
----


===== Create the example application

For the first step of our practical example, we will create our application directory structure
and populate it with example code.

Start by creating the directory tree structure for our “hello world” application:

[source, bash]
----
mkdir -p src/myapp
touch src/myapp/core.cljs
----

Second, write the example code into the previously created `src/myapp/core.cljs` file:

[source, clojure]
----
(ns myapp.core
  (:require [cljs.nodejs :as nodejs]))

(nodejs/enable-util-print!)

(defn -main
  [& args]
  (println "Hello world!"))

(set! *main-cli-fn* -main)
----

NOTE: It is very important that the declared namespace in the file exactly matches the directory
structure. This is the way _ClojureScript_ structures its source code.


===== Compile the example application

In order to compile that source code, we need a simple build script that tells the
_ClojureScript_ compiler the source directory and the output file. _ClojureScript_ has a
lot of other options, but at this moment we can ignore that.

Let’s create the _build.clj_ file with the following content:

[source, clojure]
----
(require 'cljs.closure)

(cljs.closure/build "src"
 {:output-to "main.js"
  :main 'myapp.core
  :target :nodejs})
----

This is a brief explanation of the compiler options used in this example:

* The `:output-to` parameter indicates to the compiler the destination of the compiled code, in this
  case to the "main.js" file.
* The `:main` property indicates to the compiler the namespace that will act as the entry point of
  your application when it's executed.
* The `:target` property indicates the platform where you want to execute the compiled code. In this case,
  we are going to use *iojs* (formerly nodejs). If you omit this parameter, the source will be
  compiled to run in the browser environment.

To run the compilation, just execute the following command:

[source, bash]
----
java -cp cljs.jar:src clojure.main build.clj
----

And when it finishes, execute the compiled file using *iojs*:

[source, shell]
----
$ iojs main.js
Hello world!
----


==== Compile for the Browser

In this section we are going to create an application similar to the "hello world" example from the
previous section to run in the browser environment. The minimal requirement for this application
is just a browser that can execute JavaScript.

The process is almost the same, and the directory structure is the same. The only things that
changes is the entry point of the application and the build script. So, start re-creating
the directory tree from previous example in a different directory.

Then, write new content to the `src/myapp/core.cljs` file:

[source, clojure]
----
(ns myapp.core)

(enable-console-print!)

(println "Hello world!")
----

In the browser environment we do not need a specific entry point for the application,
so the entry point is the entire namespace.


===== Compile the example application

In order to compile the source code to run properly in a browser, overwrite
the _build.clj_ file with the following content:

[source, clojure]
----
(require 'cljs.closure)

(cljs.closure/build "src"
 {:output-to "main.js"
  :output-dir "out/"
  :source-map "main.js.map"
  :main 'myapp.core
  :optimizations :none})
----

This is a brief explanation of the compiler options we're using:

* The `:output-to` parameter indicates to the compiler the destination of the compiled code, in this
  case the "main.js" file.
* The `:main` property indicates to the compiler the namespace that will act as the entry point of
  your application when it's executed.
* `:source-map` indicates the destination of the source map. (The source map connects the ClojureScript source
  to the generated JavaScript so that error messages can point you back to the original source.)
* `:output-dir` indicates the destination directory for all file sources used in a compilation. It is
  just for making source maps work properly with the rest of the code, not only your source.
* `:optimizations` indicates the compilation optimization. There are different values for this option,
  but that will be covered in subsequent sections in more detail.

To run the compilation, just execute the following command:

[source, bash]
----
java -cp cljs.jar:src clojure.main build.clj
----

This process can take some time, so do not worry; wait a little bit. The JVM bootstrap with the Clojure
compiler is slightly slow. In the following sections, we will explain how to start a watch process to avoid
constantly starting and stopping this slow process.

While waiting for the compilation, let's create a dummy HTML file to make it easy to execute our example
app in the browser. Create the _index.html_ file with the following content; it goes in the main _myapp_
directory.

[source, html]
----
<!DOCTYPE html>
<html>
  <header>
    <meta charset="utf-8" />
    <title>Hello World from ClojureScript</title>
  </header>
  <body>
    <script src="main.js"></script>
  </body>
</html>
----

Now, when the compilation finishes and you have the basic HTML file you can just open it with your favorite
browser and take a look in the development tools console. The "Hello world!" message should appear there.


==== Watch process

Surely, you have already experienced the slow startup of the _ClojureScript_ compiler. To solve this,
the _ClojureScript_ standalone compiler also comes with tools to start a process that watches the changes
in some directory and performs an incremental compilation.

Start creating another build script, but in this case, name it _watch.clj_:

[source, clojure]
----
(require 'cljs.closure)

(cljs.closure/watch "src"
 {:output-to "main.js"
  :output-dir "out/"
  :source-map "main.js.map"
  :main 'myapp.core
  :optimizations :none})
----

Now, execute that script like any other that you have executed in previous sections:

[source, bash]
----
$ java -cp cljs.jar:src clojure.main watch.clj
Building ...
Reading analysis cache for jar:file:/home/niwi/cljsbook/playground/cljs.jar!/cljs/core.cljs
Compiling out/cljs/core.cljs
Using cached cljs.core out/cljs/core.cljs
... done. Elapsed 0.8354759 seconds
Watching paths: /home/niwi/cljsbook/playground/src

Change detected, recompiling ...
Compiling src/myapp/core.cljs
Compiling out/cljs/core.cljs
Using cached cljs.core out/cljs/core.cljs
... done. Elapsed 0.191963443 seconds
----

You can observe that in the second compilation, the time is drastically reduced. Another advantage
of this method is that it gives a little bit more output.


==== Optimization levels

The _ClojureScript_ compiler has different level of optimizations. Behind the scenes, those compilation
levels are coming from Google Closure Compiler.

A very simplified overview of the compilation process is:

1. The reader reads the code and makes some analysis. This process can raise some warnings
   during this phase.
2. Then, the _ClojureScript_ compiler emits JavaScript code. The result is one JavaScript
   file for each cljs file.
3. The generated files passes through the Closure Compiler that, depending on the optimization level
   and other options (sourcemaps, output dir output to, ...) generates the final output.

The final output depends strictly on the optimization level.


===== none

Implies that closure compiler just writes the files as is, without any additional
optimization applied to the source code. This optimization level is mandatory if you are targeting
*nodejs* or *iojs* and is appropiate in development mode when your code targets the browser.


===== whitespace

This optimization level consists of concatenating the compiled files in an appropriate order, removing
line breaks and other whitespace and generating the output as one large file.

It also has some compilation speed penalty, resulting in slower compilations. In any case, it is not
terribly slow and is completely usable in small/medium applications.


===== simple

The simple compilation level implies (includes) all transformations from whitespace optimization and
additionally performs optimizations within expressions and functions, including renaming local variables
and function parameters to shorter names.

Compilation with the `:simple` optimization always preserves the functionality of syntactically valid
JavaScript, so it does not interfere with the interaction between the compiled _ClojureScript_ and
other JavaScript.


===== advanced

TBD


=== Working with the REPL

////
TODO: maybe it would be interesting to take some ideas from http://www.alexeberts.com/exploring-the-clojurescript-repl/
////


==== Introduction

Although you can create a source file and compile it every time you want to try something out in
ClojureScript, it's easier to use the REPL. REPL stands for:

* Read - get input from the keyboard
* Evaluate the input
* Print the result
* Loop back for more input

In other words, the REPL lets you try out ClojureScript concepts and get immediate feedback.

_ClojureScript_ comes with support for executing the REPL in different execution environments,
each one has its own advantages or disadvantages. For example, you can run a REPL in nodejs
but in that environment you don't have access to the DOM.

In summary: everthing really depends on your needs or requirements.


==== Nashorn REPL

The Nashorn REPL is the easiest and maybe most painless REPL environment because it does not require any
special stuff, just the JVM (JDK 8) that you have used in previous examples for running the
_ClojureScript_ compiler.

Let’s start creating the _repl.clj_ file with the following content:

[source, clojure]
----
(require '[cljs.repl]
         '[cljs.repl.nashorn])

(cljs.repl/repl
 (cljs.repl.nashorn/repl-env)
 :output-dir "out"
 :cache-analysis true)
----

Then, execute the following command to get the REPL up and running:

[source, bash]
----
$ java -cp cljs.jar:src clojure.main repl.clj
To quit, type: :cljs/quit
cljs.user=> (+ 1 2)
3
----

You may have noticed that the REPL does not have support for history and other shell-like facilities. This
is because the default REPL does not comes with "readline" support. But this problem can be solved using
the simple tool called `rlwrap` that you can certainly find with the package manager of your operating system.

The `rlwrap` makes the REPL "readline" capable and will allow you to have command history, code navigation,
and other shell-like utilities that will make your REPL experience much more pleasant. To use it, just
prepend it to the previous command that we have used to execute the REPL:

[source, bash]
----
$ rlwrap java -cp cljs.jar:src clojure.main repl.clj
To quit, type: :cljs/quit
cljs.user=> (+ 1 2)
3
----


==== Node REPL

This REPL uses nodejs/iojs as an execution environment and obviously requires that nodejs or iojs will
be installed in your system.

Surely you are asking yourself, why do I need a repl with nodejs when we have nashorn that does not require
any additional dependencies. The answer is very simple: node/iojs is the most used JavaScript execution
environment on the backend, and it has a great amount of community packages around it.

Well, the good notice is that start nodejs/iojs repl is very very easy once you have it installed in
your system. Start writing this content to a new `repl.clj` file:

[source, clojure]
----
(require '[cljs.repl]
         '[cljs.repl.node])

(cljs.repl/repl
 (cljs.repl.node/repl-env)
 :output-dir "out"
 :cache-analysis true)
----

And start the REPL like you have done it previously with nashorn REPL:

[source,bash]
----
$ rlwrap java -cp cljs.jar:src clojure.main repl.clj
To quit, type: :cljs/quit
cljs.user=> (+ 1 2)
3
----


==== Browser REPL

This REPL is the most laborious to get up and running. This is because it uses a browser for
its execution environment and it has additional requirements.

Let’s start by creating a file named `brepl.clj` with the following content:

[source, clojure]
----
(require 'cljs.repl)
(require 'cljs.closure)
(require 'cljs.repl.browser)

(cljs.closure/build
 "src"
 {:output-to "main.js"
  :output-dir "out/"
  :source-map "main.js.map"
  :main 'myapp.core
  :optimizations :none})

(cljs.repl/repl
 (cljs.repl.browser/repl-env)
 :watch "src"
 :output-dir "out/")
----

This script builds the source, just as we did earlier, and then starts the REPL.

But the browser REPL also requires that some code be executed in the browser before getting the
REPL working. To do that, just re-create the application structure very similar to the one
that we have used in previous sections:

[source, bash]
----
mkdir -p src/myapp
touch src/myapp/core.cljs
----

Then, write new content to the `src/myapp/core.cljs` file:

[source, clojure]
----
(ns myapp.core
 (:require [clojure.browser.repl :as repl]))

(defonce conn
  (repl/connect "http://localhost:9000/repl"))

(enable-console-print!)

(println "Hello, world!")
----

And finally, create the missing _index.html_ file that is going to be used as the entry point
for running the browser side code of the REPL:

[source, html]
----
<!DOCTYPE html>
<html>
  <header>
    <meta charset="utf-8" />
    <title>Hello World from ClojureScript</title>
  </header>
  <body>
    <script src="main.js"></script>
  </body>
</html>
----

Well, that was a lot of setup! But trust us, it’s all worth it when you see it in action. To do that,
just execute the `brepl.clj` in the same way that we have done it in previous examples:

[source, bash]
----
$ rlwrap java -cp cljs.jar:src clojure.main brepl.clj
Compiling client js ...
Waiting for browser to connect ...
----

And finally, open your favourite browser and go to http://localhost:9000/. Once the page
is loaded (the page will be blank), switch back to the console where you have run the REPL
and you will see that it is up and running:

[source, bash]
----
[...]
Watch compilation log available at: out/watch.log
To quit, type: :cljs/quit
cljs.user=> (+ 14 28)
42
----

One of the big advantages of this REPL is that you have access to all the browser environment. To
demonstrate it, just enter `(js/alert "hello world")` in the repl. The result of that will be
the typical browser alert dialog.


=== Build and Dependency management tools

Until now, we have used the builtin _ClojureScript_ toolchain to compile our sources to JavaScript.
It is OK to start using it and to understand how it works. But it is not very comfortable for use
on big projects with dependencies to third party libraries.

This chapter intends to explain how you can use *Leiningen*, the defacto clojure build and dependency
management tool for build _ClojureScript_ projects. It there an other growing in popularity build tool
called *boot* but at this momenent it will not  be covered in this book.


==== Installing leiningen

The installation process of leiningen is quite simple; just follow these steps:

[source, bash]
----
mkdir ~/bin
cd ~/bin
wget https://raw.githubusercontent.com/technomancy/leiningen/stable/bin/lein
chmod a+x ./lein
export PATH=$PATH:~/bin
----

Make sure that the `~/bin` directory is always set on your path. To make it permanent, add the
line starting with ` export` to your `~/.bashrc` file (we are supposing that you are using the bash shell).

Now, open another clean terminal and execute `lein version`:

[source, bash]
----
$ lein version
Leiningen 2.5.1 on Java 1.8.0_45 OpenJDK 64-Bit Server VM
----

NOTE: We are supposing that you are using a Unix-like system such as Linux or BSD. If you are
a Windows user, please check the instructions on the link:http://leiningen.org/[Leiningen homepage].
You can also get the Linux/Mac OSX/BSD version of the leiningen script at the web site.


==== First project

In this first project, we will *not* do “Hello World”&#8212;that has been done to death. Instead,
this project will be a web page that asks you for your age in years and tells you how many days
that is, using an approximation of 365 days per year.

In this project, we will use the _figwheel_ template to build the project structure. Let’s call
the project `age` and create it by typing:

[source,bash]
----
$ lein new figwheel age
Retrieving figwheel/lein-template/0.3.5/lein-template-0.3.5.pom from clojars
Retrieving figwheel/lein-template/0.3.5/lein-template-0.3.5.jar from clojars
Generating fresh 'lein new' figwheel project.
$ cd age # move into newly created project directory
----

The project has the following structure:

----
age
├── .gitignore
├── project.clj
├── README.md
├── resources
│   └── public
│       ├── css
│       │   └── style.css
│       └── index.html
└── src
    └── age
        └── core.cljs
----

The `project.clj` file contains information that Leiningen uses to download dependencies and build the project. For
now, just trust that everything in that file is exactly as it should be.

Open the `index.html` file and add the `<meta>` element to the head of the document, and modify
the body as follows:

[source,html]
----
<!DOCTYPE html>
<html>
  <head>
    <link href="css/style.css" rel="stylesheet" type="text/css">
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8" />
  </head>
  <body>
    <div id="app">
      <h1>Age in Days</h1>
      <p>Enter your age in years:
        <input type="text" size="5" id="years" /></p>
      <p id="feedback"></p>
    </div>
    <script src="js/compiled/age.js" type="text/javascript"></script>
  </body>
</html>
----

The `core.cljs` file is where all the action takes place. For now, leave it exactly as it
is, and start the figwheel environment, which will load a large number of dependencies
and start a server.

[source,bash]
----
[age] $ lein fighwheel
# much output
Prompt will show when figwheel connects to your application
----

If you are using Linux or Mac OS X, type the command as `rlwrap lein figwheel`
In your browser, go to URL `http://localhost:3449`, and you will see something like
the following screenshot if you open up the web console.

image::localhost1.png[Screenshot of web page and console]

The terminal will then give you a REPL prompt:

[source,bash]
----
To quit, type: :cljs/quit
cljs.user=> 
----

For now, do what it says in the `core.cljs` file -- change the `(println...)` and then save the file. When you do so, you will see the change reflected immediately in the browser.

Then make an error by adding an extra closing parenthesis to the `println`. When you save the file,
will see a compile error in the browser window.

image::localhost2.png[Screenshot of browser error message]

==== Interacting with JavaScript

In the REPL window, type the following to invoke JavaScript’s `window.alert()` function:

[source]
----
cljs.user=> (.alert js/window "It works!")
nil
----

The general format for invoking a JavaScript function from ClojureScript is to give the function name (preceded by a dot), the object that “owns” the function, and any parameters to that function.
You should see an alert appear in your browser winodw; when you dismiss the alert, the REPL will
print `nil` and give you another prompt. You can also do it this way:

[source]
----
cljs.user=> (js/alert "It works!")
nil
----

However, the first version always works, so, for consistency, we will use that notation throughout.
You can create objects by using their class name followed by a dot, and you can
call methods on any JavaScript objects:

[source]
----
cljs.user=> (def d (js/Date.))
#inst "2015-06-30T01:38:21.764-00:00"
cljs.user=> (.getFullYear d)
2015
cljs.user=> (.toUpperCase "abc")
"ABC"
cljs.user=> (.getElementById js/document "years")
#<[object HTMLInputElement]>
----

From that last example, you can see where we’re going. To retrieve an object’s property, use a `.-`
before the property name. In the browser window, type a number into the input field
(in the example, we typed `24`), then do this in the REPL.

[source]
----
cljs.user=> ;; for convenience define a variable
cljs.user=> (def year-field (.getElementById js/document "years"))
#<[object HTMLInputElement]>
cljs.user=> (.-value year-field)
"24"
cljs.user=> (set! (.-value year-field) "25")
"25"
----

This works, but it is little more than direct translation of JavaScript to ClojureScript. The next
step is to add event handling to the button. Event handling is loaded with all sorts of cross-platform
compatibility issues, so we’d like one step up from plain ClojureScript.  The solution is the Google Closure library.

=== The Closure Library

The good people at Google wrote a library named _Closure_ that does all that hard cross-platform
work for you. _Closure_ is also the name of their optimizing compiler that generates JavaScript that 
only includes the functions you need from the libraries that you use.

To use Closure, you have to modify the `:require` clause at the beginning of `core.cljs`

[source,clojure]
----
(ns ^:figwheel-always age.core
    (:require [goog.dom :as dom]
              [goog.events :as events]))
----

Getting an element and setting its value is slightly easier. Do this in the REPL and see the
results in the browser window.

[source]
----
cljs.user=> (in-ns 'age.core)
age.core=> (def y (dom/getElement "years"))
#<[object HTMLInputElement]>
age.core=> (set! (.-value y) "26")
"26"
age.core=> (dom/setTextContent (dom/getElement "feedback") "This works!")
----

To add an event, you define a function that takes a single argument (the event to be handled),
and then tell the appropriate HTML element to listen for it. the `events/listen` function takes
three arguments: the element to listen to, the event to listen for, and the function that will
handle the event.

[source]
----
age.core=> (defn testing [evt] (js/alert "Responding to click"))
#<function age$core$testing(evt){
return alert("Responding to click");
}>
age.core=> (events/listen (dom/getElement "calculate") "click" testing)
#<[object Object]>
----

After doing this, the browser should respond to a click on the button.
If you would like to remove the listener, use `unlisten`.

[source]
----
age.core=> (events/unlisten (dom/getElement "calculate") "click" testing)
true
----

Now, put that all together in the `core.cljs` file as follows:

[source, clojure]
----
(ns ^:figwheel-always age.core
    (:require [goog.dom :as dom]
              [goog.events :as events]))

(enable-console-print!)

(defn calculate [event]
  (let [years (.parseInt js/window (.-value (dom/getElement "years")))
        days (* 365 years)]
        (dom/setTextContent (dom/getElement "feedback")
          (str "That is " days " days old."))))

(defn on-js-reload [])

(events/listen (dom/getElement "calculate") "click" calculate)
----

=== Browser based development

TBD

//^ A little guide and notes about implications of developing for web
//^ environments (optimizations, third party libraries, modularization, ...)

==== Using third party JavaScript libraries

TBD


====  Modularizing your code

//^ Mainly related to Google Closure modules and slightly related to web-based development
//^ Maybe this is not the chapter for this section.

TBD



=== Developing a library

//^ A little guide and implications of developing a library for clojurescript.

TBD


=== Unit testing

TBD
